// Parameters
real dt = 0.1;
real mu = 0.01;

// Mesh
mesh Th = square(16, 16);

// Fespace
fespace Vh(Th, P1);
Vh u, v, uu, f, g;

// Problem
problem dHeat (u, v)
	= int2d(Th)(
		  u*v
		+ dt*mu*(dx(u)*dx(v) + dy(u)*dy(v))
	)
	+ int2d(Th)(
		- uu*v
		- dt*f*v
	)
	+ on(1, 2, 3, 4, u=g)
	;

// Time loop
real t = 0;
uu = 0;
for (int m = 0; m <= 3/dt; m++){
	// Update
	t = t+dt;
	f = x^4 - mu*t*12*x^2;
	g = t*x^4;
	uu = u;

	// Solve
	dHeat;

	// Plot
	plot(u, wait=true);
	cout << "t=" << t << " - L^2-Error=" << sqrt(int2d(Th)((u-t*x^4)^2)) << endl;
}
